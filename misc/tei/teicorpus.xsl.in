<xsl:stylesheet version="1.0" 
		xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
		xmlns:xl="http://www.w3.org/1999/xlink"
		xmlns:tei="http://www.tei-c.org/ns/1.0"
		xmlns="http://www.tei-c.org/ns/1.0"
		>

<!-- @SUMMARY@= Concatenate component files to create a &lt;teiCorpus&gt; document. -->

<xsl:param name="version" select="1.0"/>
<xsl:param name="project"/>
<xsl:output method="xml" indent="yes" encoding="utf-8"/>

<xsl:template match="/">
  <xsl:param name="datestamp" 
	     select="document('http://@@ORACC_HOST@@/datestamp')/*"/>
  <teiCorpus>
    <teiHeader>
      <fileDesc>
	<titleStmt>
	  <title>TEI version of <xsl:value-of select="$project"/> corpus</title>
	  <respStmt>
	    <resp>generated by</resp>
	    <name type="program">xtf2tei.xsl 1.0</name>
	  </respStmt>
	</titleStmt>
	<publicationStmt>
	  <availability><p>Electronic manuscript</p></availability>
	  <date><xsl:value-of select="$datestamp"/></date>
	</publicationStmt>
	<sourceDesc>
	  <p>This is the concatenation of all TEI files in the project with additional
	     metadata included from the project's glossaries.</p>
	  <p><xsl:copy-of select="document('../results/listNym.xml',/)"/></p>
	  <p><xsl:copy-of select="document('../results/listPerson.xml',/)"/></p>
	</sourceDesc>
      </fileDesc>
    </teiHeader>
    <xsl:apply-templates/>
  </teiCorpus>
</xsl:template>

<xsl:template match="item">
  <xsl:variable name="file" select="concat('../web/views/tei/',text(),'.xml')"/>
  <xsl:choose>
    <xsl:when test="count(document($file,/)/*) > 0">
      <xsl:copy-of select="document($file,/)/*"/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:message>corpus.xsl: File `<xsl:value-of select="$file"/>' not found</xsl:message>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

</xsl:stylesheet>
