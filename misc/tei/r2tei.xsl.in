<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet 
    xmlns="http://www.tei-c.org/ns/1.0"
    xmlns:b="http://oracc.org/ns/bib/1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0"
    exclude-result-prefixes="b">

<xsl:output method="xml" encoding="utf-8"/>

<xsl:template match="/">
  <xsl:call-template name="TEI-wrapper"/>
</xsl:template>

<xsl:template match="b:biblio">
  <listBibl>
    <xsl:apply-templates/>
  </listBibl>
</xsl:template>

<xsl:template match="b:record">
  <biblStruct>
    <xsl:attribute name="xml:id">
      <xsl:value-of select="b:id_biblio"/>
    </xsl:attribute>
    <xsl:choose>
      <xsl:when test="b:journal_title">
	<analytic>
	  <title><xsl:apply-templates select="b:title_of_chapter"/></title>
	  <xsl:for-each select="b:author1|b:author2|b:author3|b:author3">
	    <author><xsl:apply-templates/></author>
	  </xsl:for-each>
	</analytic>
	<monogr>
	  <title level="j">
	    <xsl:value-of select="b:journal_title"/>
	  </title>
	  <imprint>
	    <biblScope type="vol"><xsl:value-of select="b:volume_number"/></biblScope>
	    <date><xsl:value-of select="b:year_of_publication"/></date>
	    <biblScope type="pages"><xsl:value-of select="b:page_numbers"/></biblScope>
	  </imprint>
	</monogr>
	<xsl:call-template name="notes"/>
      </xsl:when>
      <xsl:when test="b:title_of_chapter">
	<analytic>
	  <title><xsl:apply-templates select="b:title_of_chapter"/></title>
	  <xsl:for-each select="b:author1|b:author2|b:author3|b:author4">
	    <author><xsl:apply-templates/></author>
	  </xsl:for-each>
	</analytic>
	<monogr>
	  <title level="m">
	    <xsl:value-of select="b:book_title"/>
	  </title>
	  <xsl:for-each select="b:editor1|b:editor2|b:editor3|b:editor4">
	    <editor><xsl:apply-templates/></editor>
	  </xsl:for-each>
	  <imprint>
	    <pubPlace><xsl:value-of select="b:place_published"/></pubPlace>
	    <publisher><xsl:value-of select="b:publisher"/></publisher>
	    <date><xsl:value-of select="b:year_of_publication"/></date>
	    <biblScope type="pages"><xsl:value-of select="b:pages"/></biblScope>
	  </imprint>
	</monogr>
	<xsl:if test="b:book_series_title">
	  <series>
	    <title>
	      <xsl:value-of select="b:book_series_title"/>
	    </title>
	    <biblScope type="number"><xsl:value-of select="b:number_in_series"/></biblScope>
	  </series>
	</xsl:if>
      </xsl:when>
      <xsl:when test="b:book_title">
	<monogr>
	  <title><xsl:apply-templates mode="ok" select="b:book_title"/></title>
	  <xsl:for-each select="b:author1|b:author2|b:author3|b:author4">
	    <author><xsl:apply-templates/></author>
	  </xsl:for-each>
	  <imprint>
	    <pubPlace><xsl:value-of select="b:place_published"/></pubPlace>
	    <publisher><xsl:value-of select="b:publisher"/></publisher>
	    <date><xsl:value-of select="b:year_of_publication"/></date>
	  </imprint>
	</monogr>
	<xsl:if test="b:titles/b:secondary-title">
	  <series>
	    <title>
	      <xsl:value-of select="b:titles/b:secondary-title"/>
	    </title>
	    <biblScope type="number"><xsl:value-of select="b:number"/></biblScope>
	  </series>
	</xsl:if>
      </xsl:when>
      <xsl:when test="b:unpub_title">
	<monogr>
	  <title level="u"><xsl:apply-templates mode="ok" select="b:unpub_title"/></title>
	  <xsl:for-each select="b:author1|b:author2|b:author3|b:author4">
	    <author><xsl:apply-templates/></author>
	  </xsl:for-each>
	  <imprint>
	    <pubPlace><xsl:value-of select="b:place_published"/></pubPlace>
	    <publisher><xsl:value-of select="b:publisher"/></publisher>
	    <date><xsl:value-of select="b:year_of_publication"/></date>
	  </imprint>
	</monogr>
      </xsl:when>
      <xsl:when test="shortref">
	<!-- abbreviation stubs; drop these -->
      </xsl:when>
      <xsl:otherwise>
	<xsl:message>Mysterious record id=<xsl:value-of select="@xml:id"/></xsl:message>
      </xsl:otherwise>
    </xsl:choose>
    <xsl:apply-templates select="b:treats"/>
  </biblStruct>
</xsl:template>

<xsl:template name="notes">
  <xsl:if test="string-length(b:cat_no.)>0">
    <note type="cat_no"><xsl:value-of select="cat_no."/></note>
  </xsl:if>
  <xsl:if test="string-length(b:contents)>0">
    <note type="content"><xsl:value-of select="contents"/></note>
  </xsl:if>
</xsl:template>

<xsl:template match="b:treats">
  <xsl:apply-templates/>
</xsl:template>

<xsl:template match="b:source">
  <relatedItem type="tabletRef">
    <msDesc>
      <msIdentifier>
	<idno type="cdli-p-number"><xsl:apply-templates select="b:id_text"/></idno>
	<msName type="designation"><xsl:apply-templates select="b:designation"/></msName>
      </msIdentifier>
      <msContents>
	<p><xsl:apply-templates select="b:subgenre"/></p>
      </msContents>
      <history>
	<origin>
	  <origPlace><xsl:value-of select="b:findspot_remarks"/></origPlace>
	</origin>
      </history>
      <additional>
	<listBibl>
	  <bibl>
	    <biblScope type="pages"><xsl:value-of select="b:pages"/></biblScope>
	  </bibl>
	</listBibl>
      </additional>
    </msDesc>
  </relatedItem>
</xsl:template>

<xsl:template name="TEI-wrapper">
  <TEI>
    <teiHeader>
      <fileDesc>
        <titleStmt>
          <title>TEI listBibl from cams/gkab</title>
          <respStmt>
            <resp>generated by</resp>
            <name type="program">r2tei.xsl 1.0</name>
          </respStmt>
        </titleStmt>
        <publicationStmt>
          <availability>
            <p>Electronic manuscript</p>
          </availability>
          <date><xsl:value-of select="document('http://@@ORACC_HOST@@/datestamp')/*"/></date>
        </publicationStmt>
        <sourceDesc>
          <p>This is a TEI-formatted version of the project's bibliography (which is exported as FMP XML from a FileMaker database).</p>
        </sourceDesc>
      </fileDesc>
    </teiHeader>
    <text type="listBibl">
      <body>
	<xsl:apply-templates select="b:biblio"/>
      </body>
    </text>
  </TEI>
</xsl:template>

</xsl:stylesheet>
